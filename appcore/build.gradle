import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.library'
apply from: '../config/quality.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.so'])
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.squareup.okio:okio:2.2.2'
}

android {
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    compileSdkVersion Integer.decode("$sdk_version")
    buildToolsVersion "$build_version"

    defaultConfig {
        minSdkVersion Integer.decode("$min_version")
        versionCode Integer.decode("${POM_VERSION_CODE}")
        versionName "${POM_VERSION}"
        multiDexEnabled true
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries false
        //dexInProcess  true
        jumboMode true
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    //自定义aar名称
    android.libraryVariants.all {
        variant ->
            variant.outputs.all {
                def fileName = "${POM_NAME}" + "_V" + "${POM_VERSION}" + ".aar"
                outputFileName = fileName
            }
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name == BuilderConstants.DEBUG) {
        return // Skip debug builds.
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.exclude '**/BuildConfig.class'
    task.exclude '**/R.class'
    task.manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version
        )
    }
    artifacts.add('archives', task)
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}
//
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        encoding "UTF-8"
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/properties.xml'
    destinationDir = file("../docs/")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
